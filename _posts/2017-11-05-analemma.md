---
layout: post
title: Drawing the analemma with Julia
tags: draft julia juliaastro astrolib plotting
---

You may know that if you check the position of the Sun every day in the same
place at the same time (accounting for daylight saving time if necessary),
you’ll find that it slightly moves.  This is a combination of the tilt of the
Earth’s axis and the Earth’s orbital eccentricity.  The path traced out by the
position in the sky of the Sun during its wandering is
called [analemma](https://en.wikipedia.org/wiki/Analemma).

The following is an afternoon analemma taken in 1998–99 by Jack Fishburn in
Murray Hill, New Jersey, USA
([Wikimedia Commons](https://commons.wikimedia.org/wiki/File:Analemma_fishburn.tif)).

[![Analemma Murray Hill](https://upload.wikimedia.org/wikipedia/commons/thumb/8/8c/Analemma_fishburn.tif/lossy-page1-812px-Analemma_fishburn.tif.jpg)](https://commons.wikimedia.org/wiki/File:Analemma_fishburn.tif)

We can use [Julia](https://julialang.org/) to plot the analemma.  In particular,
we’ll employ [AstroLib.jl](https://github.com/JuliaAstro/AstroLib.jl) to do the
needed calculations.  Throughout this post I’ll assume you have installed
the [latest stable version](https://julialang.org/downloads/) of Julia and the
necessary packages with
the
[built-in package manager](https://docs.julialang.org/en/stable/manual/packages).

What we want to do is to determine
the [position of the Sun](https://en.wikipedia.org/wiki/Position_of_the_Sun) for
a specific time every day in a year, say at noon for the whole 2018.  This is
the recipe:

1. compute the [Julian dates](https://en.wikipedia.org/wiki/Julian_day) of all
   the wanted times
2. calculate
   the
   [equatorial coordinates](https://en.wikipedia.org/wiki/Equatorial_coordinate_system) for
   the given Julian dates
3. convert the equatorial coordinates
   to
   [horizontal coordinates](https://en.wikipedia.org/wiki/Horizontal_coordinate_system) in
   the desired place.  For example, we
   choose [Heidelberg](https://en.wikipedia.org/wiki/Heidelberg), in Germany,
   which has coordinates 49°25′N 08°43′E and elevation of 114 m.

The trickiest part is to get the right Julian dates.
The
[`jdcnv`](https://juliaastro.github.io/AstroLib.jl/stable/ref.html#AstroLib.jdcnv) function
that we’ll use to compute them assumes that times are given
in [UTC standard](https://en.wikipedia.org/wiki/Coordinated_Universal_Time), but
Heidelberg is one hour ahead of Greenwich.  In addition, Germany adopts daylight
saving time from March to October, thus noon on May 15th is not actually the
same [time of day](https://en.wikipedia.org/wiki/Solar_time#Mean_solar_time) as
noon on November 7th.  However, noon on January 1st is the same time of day as
noon on December 31st, so we can create a range between these two times with
step one day and then we add one hour ($$1/24$$ of day) to cater for the time
zone.

```julia
using AstroLib

function analemma(start_date, end_date,
                  time_zone, latitude, longitude, elevation)
    julian_dates = (jdcnv(start_date):jdcnv(end_date)) .- time_zone/24
    right_ascension, declination = sunpos(julian_dates)
    altaz = eq2hor.(right_ascension, declination,
	                julian_dates, latitude, longitude, elevation)
    altitude = getindex.(altaz, 1)
    azimuth  = getindex.(altaz, 2)
    return azimuth, altitude
end
```

We have
used
[`sunpos`](https://juliaastro.github.io/AstroLib.jl/stable/ref.html#AstroLib.sunpos-Tuple{Real}) to
get the position of the Sun in equatorial coordinates and converted them with
`eq2hor` to horizontal coordinates, specifying the coordinates of Heidelberg.
The [broadcast version](https://julialang.org/blog/2017/01/moredots) of this
function returns an array of 2-tuples, being the first element the altitude of
the Sun and the second element its azimuth.  We’ve used `getindex.(altaz, i)` to
obtain the arrays with the `i`-th elements of the tuples.  Now we can draw the
analemma.  I recommend using
the [`Plots.jl`](https://github.com/JuliaPlots/Plots.jl) package, which provides
a single interface to several different back-ends (GR, PyPlot, PGFPlots,
etc...).

```julia
using Plots, Base.Dates

azimuth, altitude =
    analemma(DateTime(2018, 1, 1, 12),
             DateTime(2018, 12, 31, 12),
             +1, ten(49, 25), ten(8, 43), 114)

scatter(azimuth, altitude, xlabel = "Azimuth (°)", ylabel = "Altitude (°)")
```

[![Analemma Heidelberg](/img/analemma-heidelberg.png)](/img/analemma-heidelberg.png)

You can check with the [JPL HORIZONS System](https://ssd.jpl.nasa.gov/?horizons)
that this is accurate within a
few [arcminutes](https://en.wikipedia.org/wiki/Minute_and_second_of_arc).

<!-- Local Variables: -->
<!-- ispell-local-dictionary: "british" -->
<!-- End: -->
